import java.text.SimpleDateFormat

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    classpath "se.transmode.gradle:gradle-docker:$gradle_docker_version"
  }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

repositories {
  jcenter()
  mavenCentral()
}

sourceSets {
  integrationTest {
    compileClasspath += sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }

  swagger {
    compileClasspath += sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }
}

checkstyle {
  toolVersion = checkstyle_version
}

jar {
  baseName = 'spring-boot-rest-skeleton'
  version = "1.0.${System.getProperty('build.number') ?: 'unspecified'}"
}

group = 'com.aukhatov'
version = jar.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

idea {
  project {
    languageLevel = 1.8
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }
}

configurations {
  compile.exclude module: 'commons-logging'
}

springBoot {
  buildInfo {
    // Generate extra build info for Actuator /info
    additionalProperties = [
        by                   : System.properties['user.name'],
        operatingSystem      : System.properties['os.name'] + " " + System.properties['os.version'],
        time: buildTime()
    ]
  }
}

def buildTime() {
  final dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ssZ")
  dateFormat.timeZone = TimeZone.getTimeZone("Asia/Yekaterinburg")
  dateFormat.format(new Date())
}

dependencies {
  //Spring
  compile "org.springframework.boot:spring-boot-starter-actuator:$spring_boot_version"
  compile "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"
  compile "org.springframework.cloud:spring-cloud-starter-sleuth:$spring_boot_sleuth_version"

  // Misc
  compile "org.apache.commons:commons-lang3:$apache_commons_lang_version"
  compile "commons-io:commons-io:$apache_commons_io_version"
//  compile "de.condecentric:spring-boot-admin-starter-client:$spring_boot_admin_version"

  // Swagger
  compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.13'
  swaggerCompile "io.springfox:springfox-swagger2:$swagger_version"

  testCompile "junit:junit:$junit_version"
  testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
  testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
  testCompile "org.springframework.boot:spring-boot-starter-test:$spring_boot_version"
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task integrationTest(type: Test, dependsOn: ['integrationTestClasses'], description: 'Run integration tests') {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
}

task swagger(type: Test, dependsOn: ['swaggerClasses'], description: 'Generate swagger.json') {
  testClassesDir = sourceSets.swagger.output.classesDir
  classpath = sourceSets.swagger.runtimeClasspath
  outputs.upToDateWhen { false }
}

tasks.withType(Checkstyle) {
  exclude '**/integrationTest/**'
  maxWarnings = 0
}

task wrapper(type: Wrapper) {
  gradleVersion = gradle_version
}

task buildWar(type: War) {

  dependsOn += tasks['swagger']
  webInf {
    into('classes') {
      from sourceSets.main.output
    }
  }

  version = jar.version
  baseName = jar.baseName
}

task buildDocker(type: Docker, dependsOn: build) {
  applicationName = jar.baseName
  dockerfile = file("${projectDir}/src/main/docker/Dockerfile")
  doFirst {
    copy {
      from jar
      into stageDir
    }
    copy {
      from "${projectDir}/src/main/docker"
      into stageDir
    }
  }
}